//Needed for ADD_SPELL_HEADER.
INCLUDE ~spell_rev/lib/macros.tpa~

//Add sectype.
ADD_SECTYPE "k1#Dispel" @21000

//Create k1#scre -> TODO: this spell can be added statically.
LAF CREATE_SPELL STR_VAR spell = "k1#scre" END
COPY_EXISTING ~k1#scre.spl~ ~override~
	SAY NAME1 @21001
	SAY UNIDENTIFIED_DESC @21001
	//Set sectype and level.
	WRITE_BYTE 0x27 k1#Dispel
	WRITE_LONG 0x34 1
	//Create header and then set its fields via ALTER_SPELL_HEADER.
	LPF ADD_SPELL_HEADER INT_VAR type = 1 END
	LPF ALTER_SPELL_HEADER INT_VAR
		header = 1
		location = 0                        //Subpell.
		target = 1                          //Living actor.
		min_level = 1
		speed = 1                           //Cast instantly, so should not matter.
		range = 30 							//Uses projectile.
		projectile = 158                    //TODO => move spell to parent and make this a subspell.
	END
	//Add opcodes.
    //Immunity to Breach.
	LPF ADD_SPELL_EFFECT INT_VAR
		opcode = 206 	 					//Protection from spell.
		target = 2
		power = 5
		timing = 0
		duration = 300
		resist_dispel = 2
	STR_VAR
		resource = "spwi513b"               //Aux breach spell.
	END
	//Immunity to Dispel.
 	LPF ADD_SPELL_EFFECT INT_VAR
		opcode = 101                        //Immuny to opcode.
		parameter2 = 58                     //Dispel.
		target = 2
		power = 5
		timing = 0
		duration = 300
		resist_dispel = 2
    END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 142                        //Display portrait icon.
        parameter2 = 107                    //Spell immunity
        target = 2
        power = 5
        timing = 0
        duration = 300
        resist_dispel = 2
    END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 296                        //Protection from animation.
        target = 2
        power = 5
        timing = 0
        duration = 300
        resist_dispel = 2
    STR_VAR
        resource = "spdispma"
    END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 215                        //Play visual effect.
        parameter2 = 1                      //Over target.
        power = 5
        target = 2
        timing = 0
        duration = 300
        resist_dispel = 2
    STR_VAR
        resource = "spturni2"
    END
    LPF ADD_SPELL_EFFECT INT_VAR
        opcode = 267                        //Immunity from string.
        parameter1 = 14056                  //'Dispel effects'
        target = 2
        power = 5
        timing = 0
        duration = 300
        resist_dispel = 2
    END

WITH_SCOPE BEGIN
	ACTION_FOR_EACH res IN "spwi510" "spwi590" BEGIN
		COPY_EXISTING ~%res%.spl~ ~override~
			//Delete all opcodes => relies on k1#scre having everything right.
			LPF DELETE_SPELL_EFFECT INT_VAR
				opcode_to_delete = 0 - 1
			END
			LPF ADD_SPELL_EFFECT INT_VAR
				opcode = 146 						//Cast spell.
				parameter2 = 1 						//Cast instantly.
				target = 1 							//Spell has caster target.
				power = 5 
			STR_VAR
				resource = "k1#scre"
			END
			LPF ADD_SPELL_EFFECT INT_VAR
				opcode = 215 						//Play visual effect.
				parameter2 = 1
				target = 2 							//Will only target caster...
				power = 5
			STR_VAR
				resource = "sprotect"
			END
		BUT_ONLY
	END
END

//Spell Shield: remove immunity against aux breach. Why?
COPY_EXISTING ~spwi519.spl~ ~override~
	LPF DELETE_EFFECT INT_VAR
		match_opcode = 206
	STR_VAR
		match_resource = "spwi513b"
	END
BUT_ONLY

//Patch items anspells to take into account dispelling screen.
COPY_EXISTING_REGEXP GLOB ~.*\.itm~ ~override~
	PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
		LPF CLONE_EFFECT INT_VAR
			check_globals = 0 				//Only change effects in headers.
			match_opcode = 58 				//Dispel effects.
			opcode = 221 					//Remove sectype.
			parameter1 = 9
			parameter2 = k1#Dispel
			timing = 1
			duration = 0 					//Zero-out field.
			silent = 1
		STR_VAR
			insert = "below"
		END
	END
BUT_ONLY

COPY_EXISTING_REGEXP GLOB ~.*\.spl~ ~override~
	PATCH_IF (SOURCE_SIZE > 0x71) BEGIN
		LPF CLONE_EFFECT INT_VAR
			check_globals = 0 				//Only clone effects in headers.
			match_opcode = 58 				//Dispel effects.
			opcode = 221 					//Remove effects by sectype.
			parameter1 = 9 					//Level 9.
			parameter2 = k1#Dispel
			timing = 1
			duration = 0 					//Zero-out field.
			silent = 1
		STR_VAR
			insert = "below"
		END
	END
BUT_ONLY

//Breach aux spells.
ACTION_IF FILE_EXISTS_IN_GAME "spwi513c.spl" BEGIN
	COPY_EXISTING ~spwi513c.spl~ ~override~
		//What is this for?
		LPF ALTER_SPELL_EFFECT INT_VAR
			match_opcode = 230
			new_opcode = 221
			parameter1 = 9
			parameter2 = k1#Dispel
		END
	BUT_ONLY
END ELSE BEGIN
    WARN "Warning: resource spwi513c does not exist."
END

//Pierce shield.
WITH_SCOPE BEGIN
    ACTION_FOR_EACH res IN "spwi805b" "spwi903b" BEGIN
        ACTION_IF FILE_EXISTS_IN_GAME ~%res%.spl~ BEGIN
            //Pierce Shield breaks DS; Spell shield stops it however.
            COPY_EXISTING ~spwi805b.spl~ ~override~
                LPF ADD_SPELL_EFFECT INT_VAR
                    opcode = 221                    // remove type protection
                    target = 2
                    power = 8
                    parameter1 = 9
                    parameter2 = k1#Dispel
                    timing = 1
                END
            BUT_ONLY
        END ELSE BEGIN
            WARN "Warning: resource %res% does not exist."
        END
    END
END
