DEFINE_PATCH_FUNCTION MAKE_REFRESHING INT_VAR debug = 0 STR_VAR resource = "" BEGIN
    PATCH_IF NOT (debug = 0) BEGIN
        //Debug message.
        PATCH_PRINT "%SOURCE_RES%: refresh, non-stacking on %resource%."
    END
    //Clone relevant 206 -> 321.
    LPF CLONE_EFFECT INT_VAR
        match_opcode = 206              //Protection from spell.
        opcode = 321                    //Remove effects by resource.
        parameter1 = 0                  //Zero-out unused field.
        parameter2 = 0
        timing = 1                      //Instantaneous.
        duration = 0                    //Zero out unused field.
        power = 0                       //Bypass spell protections.
        resist_dispel = 0               //non-dispellable, bypass mr.
        savingthrow = 0                 //No save.
        savebonus = 0
        verbose = debug
    STR_VAR
        match_resource = EVAL "%resource%"
        insert = "first"
    END
    //Delete relevant 206.
    LPF DELETE_EFFECT INT_VAR
        match_opcode = 206              //Protection from spell.
        verbose = debug
    STR_VAR
        match_resource = EVAL "%resource%"
    END
END

//Main loop function.
DEFINE_ACTION_FUNCTION MAKE_REFRESHING_LOOP BEGIN
    WITH_SCOPE BEGIN
        //Array of spells to patch.
        ACTION_DEFINE_ARRAY spells_to_patch BEGIN
            //Divine spells.
            "CLERIC_ARMOR_OF_FAITH"
            "CLERIC_DOOM"
            "CLERIC_AID"
            "CLERIC_BARKSKIN"
            "CLERIC_CHANT"
            "CLERIC_RESIST_FIRE_AND_COLD"
            "CLERIC_KNOW_OPPONENT"
            "CLERIC_STRENGTH_OF_STONE"          //No protection opcode: fixed in spell.
            "CLERIC_DEFENSIVE_HARMONY"
            "CLERIC_HOLY_POWER"
            "CLERIC_CLOAK_OF_FEAR"
            "CLERIC_RIGHTEOUS_MAGIC"
            "CLERIC_BLADE_BARRIER"
            "CLERIC_REGENERATE"
            "CLERIC_GLOBE_OF_BLADES"            //No protection opcode: fixed in spell.
            "CLERIC_AURA_OF_FLAMING_DEATH"      //No protection opcode: fixed in spell.
            "CLERIC_REGENERATE_LIGHT_WOUNDS"
            "CLERIC_REGENERATE_MODERATE_WOUNDS"
            "CLERIC_REGENERATE_SERIOUS_WOUNDS"
            "CLERIC_REGENERATE_CRITICAL_WOUNDS"
            "CLERIC_MASS_REGENERATE"
            "CLERIC_REGENERATION_DRUID_VERSION"
            //Arcane spells.
            "WIZARD_ARMOR"
            "WIZARD_REFLECTED_IMAGE"            //No protection opcode: fixed in spell.
            "WIZARD_TRUE_STRIKE"
            "WIZARD_SHIELD"
            "WIZARD_BLUR"
            "WIZARD_KNOW_OPPONENT"
            "WIZARD_LUCK"
            "WIZARD_MIRROR_IMAGE"               //No protection opcode: fixed in spell.
            "WIZARD_RESIST_ELEMENTS"            //No protection opcode: fixed in spell.
            "WIZARD_CLAIRVOYANCE"
            "WIZARD_GHOST_ARMOR"
            //No protection opcode: fixed in spell.
            "WIZARD_MINOR_GLOBE_OF_INVULNERABILITY"
            "WIZARD_EMOTION_HOPELESSNESS"       //Emotion: Despair.
            "WIZARD_GREATER_MALISON"
            "WIZARD_SPIRIT_ARMOR"
            "WIZARD_FIRE_SHIELD_RED"            //No protection opcode: fixed in spell.
            "WIZARD_MESTILS_ACID_SHEATH"        //No protection opcode: fixed in spell.
            "WIZARD_GLOBE_OF_INVULNERABILITY"   //No protection opcode: fixed in spell.
            //No protection opcode: fixed in spell.
            "WIZARD_PROTECTION_FROM_MAGIC_ENERGY"
            //No protection opcode: fixed in spell.
            "WIZARD_PROTECTION_FROM_THE_ELEMENTS"
            "WIZARD_MANTLE"                     //No protection opcode. This is fixed in RFC.
            "WIZARD_GHOSTFORM"                  //No protection opcode: fixed in spell.
            "WIZARD_PROTECTION_FROM_ENERGY"     //No protection opcode: fixed in spell.
            "WIZARD_MOMENT_OF_PRESCIENCE"       //No protection opcode: fixed in spell.
        END

        //Main self-stacking loop: the most common case.
        ACTION_PHP_EACH spells_to_patch AS i => spell BEGIN
            OUTER_SET spell_nbr = IDS_OF_SYMBOL ("spell" "%spell%")
            ACTION_IF (spell_nbr = 0 - 1) BEGIN
                WARN "Spell %spell% not present in spell.ids. Doing nothing."
            END ELSE BEGIN
                LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = EVAL "%spell%" RET spell_res END
                COPY_EXISTING ~%spell_res%.spl~ ~override~
                    LPF MAKE_REFRESHING INT_VAR debug = 1 STR_VAR resource = EVAL "%spell_res%" END
                BUT_ONLY
            END
        END
    END

    WITH_SCOPE BEGIN
        //Concurrent refreshing.
        ACTION_DEFINE_ASSOCIATIVE_ARRAY spells_to_patch BEGIN
            "CLERIC_KNOW_OPPONENT" => "WIZARD_KNOW_OPPONENT"
            "WIZARD_KNOW_OPPONENT" => "CLERIC_KNOW_OPPONENT"
            "WIZARD_RESIST_ELEMENTS" => "CLERIC_RESIST_FIRE_AND_COLD"
            "CLERIC_RESIST_FIRE_AND_COLD" => "WIZARD_RESIST_ELEMENTS"
        END

        ACTION_PHP_EACH spells_to_patch IN spell => res BEGIN
            PRINT "Patching %spell% for concurrency with %res%."
            OUTER_SET spell_nbr_spell = IDS_OF_SYMBOL ("spell" "%spell%")
            OUTER_SET spell_nbr_res = IDS_OF_SYMBOL ("spell" "%res%")
            ACTION_IF (spell_nbr_spell = 0 - 1) OR (spell_nbr_res = 0 - 1) BEGIN
                WARN "Spell not present in spell.ids. Doing nothing."
            END ELSE BEGIN
                LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = EVAL "%spell%" RET spell_res END
                LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = EVAL "%res%" RET res = spell_res END
                COPY_EXISTING ~%spell_res%.spl~ ~override~
                    LPF MAKE_REFRESHING INT_VAR debug = 1 STR_VAR resource = EVAL "%res%" END
                BUT_ONLY
            END
        END
    END

    WITH_SCOPE BEGIN
        //Stragglers.
        //Selfstacking for subspells.
        //Chant.
        COPY_EXISTING ~sppr203d.spl~ ~override~
            LPF MAKE_REFRESHING INT_VAR debug = 1 STR_VAR resource = "sppr203d" END
        BUT_ONLY
        
        //Animal fang.
        COPY_EXISTING ~dvpr525d.spl~ ~override~
            LPF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = "CLERIC_ANIMAL_GROWTH" RET spell_res END
            LPF ALTER_EFFECT INT_VAR
                match_opcode = 206
                verbose = 1
            STR_VAR
                match_resource = EVAL "%spell_res%"
                resource = "dvpr525d"
            END
            LPF MAKE_REFRESHING INT_VAR debug = 1 STR_VAR resource = "dvpr525d" END
        BUT_ONLY

        ACTION_FOR_EACH spell IN "WIZARD_WRAITH_FORM" BEGIN
            OUTER_SET spell_nbr = IDS_OF_SYMBOL ("spell" "%spell%")
            ACTION_IF (spell_nbr = 0 - 1) BEGIN
                WARN "Spell %spell% not present in spell.ids. Doing nothing."
            END ELSE BEGIN
                LAF RES_NUM_OF_SPELL_NAME STR_VAR spell_name = EVAL "%spell%" RET spell_res END
                COPY_EXISTING ~%spell_res%.spl~ ~override~
                    LPF ADD_SPELL_EFFECT INT_VAR
                        opcode = 321                    //Remove effects by resource.
                        timing = 1                      //Instantaneous.
                        target = 1                      //Self.
                        insert_point = 0                //First.
                    STR_VAR
                        resource = EVAL "%spell_res%"
                    END
                BUT_ONLY
            END
        END
    END
END

//Run main loop.
LAF MAKE_REFRESHING_LOOP END
