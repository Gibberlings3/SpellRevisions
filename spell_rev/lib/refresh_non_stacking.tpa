INCLUDE ~spell_rev\lib\manage_add_spell_references.tpa~

//Fill new regeneration spell references for latter use.
LAF GET_NEW_SPELL_RESREFS RET_ARRAY new_spell_resrefs END
OUTER_TEXT_SPRINT sppr117 $new_spell_resrefs("sppr117")
OUTER_TEXT_SPRINT sppr217 $new_spell_resrefs("sppr217")
OUTER_TEXT_SPRINT sppr323 $new_spell_resrefs("sppr323")
OUTER_TEXT_SPRINT sppr419 $new_spell_resrefs("sppr419")
OUTER_TEXT_SPRINT sppr520 $new_spell_resrefs("sppr520")
OUTER_TEXT_SPRINT sppr521 $new_spell_resrefs("sppr521")
OUTER_TEXT_SPRINT sppr522 $new_spell_resrefs("sppr522")
OUTER_TEXT_SPRINT sppr523 $new_spell_resrefs("sppr523")
OUTER_TEXT_SPRINT sppr524 $new_spell_resrefs("sppr524")
OUTER_TEXT_SPRINT sppr619 $new_spell_resrefs("sppr619")
OUTER_TEXT_SPRINT spwi225 $new_spell_resrefs("spwi225")
OUTER_TEXT_SPRINT spwi526 $new_spell_resrefs("spwi526")

DEFINE_PATCH_FUNCTION MAKE_REFRESHING INT_VAR debug = 0 STR_VAR resource = "" BEGIN
    PATCH_IF NOT (debug = 0) BEGIN
        //Debug message.
        PATCH_PRINT "%SOURCE_RES%: refresh, non-stacking on %resource%."
    END
    //Clone relevant 206 -> 321.
    LPF CLONE_EFFECT INT_VAR
        match_opcode = 206              //Protection from spell.
        opcode = 321                    //Remove effects by resource.
        parameter1 = 0                  //Zero-out unused field.
        parameter2 = 0
        duration = 0                    //Zero-out unused field.
        power = 0                       //Bypass spell protections.
        resist_dispel = 0               //non-dispellable, bypass mr.
        savingthrow = 0                 //No save.
        savebonus = 0
        verbose = debug
    STR_VAR
        resource = "%resource%"
        insert = "first"
    END
    //Delete relevant 206.
    LPF DELETE_EFFECT INT_VAR
        match_opcode = 206              //Protection from spell.
        verbose = debug
    STR_VAR
        match_resource = "%resource%"
    END
END

//Spells to patch.
ACTION_DEFINE_ARRAY spells_to_patch BEGIN
    //Divine spells.
    "sppr111"                           //Armor of Faith.
    "sppr113"                           //Doom.
    "%sppr117%"                         //Regenerate Light Wounds
    "sppr201"                           //Aid.
    "sppr202"                           //Barkskin.
    "sppr203"                           //Chant.
    "sppr203d"                          //Aux, Bad chant.
    "sppr209"                           //Know Opponent, Cleric.
    "sppr210"                           //Resist Elements, Cleric.
    "%sppr217%"                         //Regenerate Moderate Wounds
    "sppr312"                           //Strength of Stone.
    "%sppr323%"                         //Regenerate Serious Wounds
    "sppr406"                           //Defensive Harmony.
    "sppr407"                           //Protection from Electricity.
    "sppr412"                           //Holy Power.
    "sppr416"                           //Cloak of Fear.
    "%sppr419%"                         //Regenerate Critical Wounds
    "sppr513"                           //Righteous Fury.
    "%sppr520%"                         //Protection from Acid.
    "%sppr521%"                         //Protection from Cold.
    "%sppr522%"                         //Protection from Electricity.
    "%sppr523%"                         //Protection from Fire.
    "%sppr524%"                         //Mass Regeneration.
    "dvpr525d"                          //Animal Growth, aux spell.
    "sppr603"                           //Blade Barrier.
    "%sppr619%"                         //Regeneration, Druid.
    "sppr711"                           //Regeneration.
    "sppr725"                           //Globe of Blades.
    "sppr730"                           //Aura of Flaming Death.
    //Mage spells.
    "spwi102"                           //Armor.
    "spwi111"                           //True Strike.
    "spwi114"                           //Shield.
    "spwi201"                           //Blur.
    "spwi208"                           //Know Opponent, Wizard.
    "spwi209"                           //Luck.
    "%spwi225%"                         //Resist Elements, Wizard.
    "spwi301"                           //Clairvoyance.
    "spwi315"                           //Wraithform.
    "spwi317"                           //Ghost Armor.
    "spwi319"                           //Protection from fire.
    "spwi320"                           //Protection from cold.
    "spwi403"                           //Mestil's Acid Shield.
    "spwi411"                           //Emotion.
    "spwi412"                           //Greater Malison.
    "spwi414"                           //Spirit Armor.
    "spwi418"                           //Fire Shield.
    "spwi512"                           //Protection from Electricity.
    "spwi517"                           //Protection from Acid.
    "%spwi526%"                         //Mestil's Acid Shield.
    "spwi602"                           //Globe of Invulnerability.
    "spwi606"                           //Protection from Magic Energy.
    "spwi702"                           //Protection from the Elements.
    "spwi708"                           //Prismatic Mantle.
    "spwi801"                           //Ghost Form.
    "spwi803"                           //Protection from Energy.
    "spwi808"                           //Moment of Presciense
END

//Main patching loop, dealing with self-stacking, the most common case.
ACTION_PHP_EACH spells_to_patch AS i => resource BEGIN
    ACTION_IF FILE_EXISTS_IN_GAME "%resource%.spl" BEGIN
        COPY_EXISTING ~%resource%.spl~ ~override~
            LPF MAKE_REFRESHING STR_VAR resource = "%resource%" END
    END
END

//TODO: regeneration spells => not concurrent with each other. Do we want that?
//ACTION_PHP_EACH regen_spells AS i => regen_spell BEGIN
//    ACTION_IF (FILE_EXISTS_IN_GAME ~%regen_spell%.spl~) BEGIN
//        COPY_EXISTING ~%regen_spell%.spl~ ~override~
//            LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 206 END
//            PHP_EACH regen_spells AS j => regen_spell_inner BEGIN
//                LPF ADD_SPELL_EFFECT INT_VAR opcode = 321 insert_point = 0 target = 2 STR_VAR resource = EVAL ~%regen_spell_inner%~ END
//            END
//    END
//END

//Know Opponent, Cleric.
ACTION_IF FILE_EXISTS_IN_GAME ~sppr209.spl~ THEN BEGIN 
    COPY_EXISTING ~sppr209.spl~ ~override~
        //Know Opponent, Wizard.
        LPF MAKE_REFRESHING STR_VAR resource = "spwi208" END
END

//Resist Elements, Cleric.
ACTION_IF FILE_EXISTS_IN_GAME ~sppr210.spl~ THEN BEGIN
    COPY_EXISTING ~sppr210.spl~ ~override~
        //Resist elements, Wizard.
        LPF MAKE_REFRESHING STR_VAR resource = EVAL "%spwi225%" END
END

//Mage spells.
//Know opponent, Wizard.
ACTION_IF FILE_EXISTS_IN_GAME ~spwi208.spl~ THEN BEGIN
    COPY_EXISTING ~sppr208.spl~ ~override~
        //Know opponent, Cleric.
        LPF MAKE_REFRESHING STR_VAR resource = "sppr209" END
END

//Mirror Image.
ACTION_IF FILE_EXISTS_IN_GAME ~spwi212.spl~ THEN BEGIN
    COPY_EXISTING ~spwi212.spl~ ~override~
        //Reflected image.
        LPF MAKE_REFRESHING STR_VAR resource = "spwi120" END
END

//Resist elements, Wizard.
ACTION_IF FILE_EXISTS_IN_GAME ~%spwi225%.spl~ THEN BEGIN
    COPY_EXISTING ~%spwi225%.spl~ ~override~
        //Resist elements, Cleric
        LPF MAKE_REFRESHING STR_VAR resource = "sppr210" END
END